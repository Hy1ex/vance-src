//  STATIC: "COLOR_DEPTH"			"0..1"
//	STATIC: "TREESWAY"				"0..2"

//	DYNAMIC: "COMPRESSED_VERTS"		"0..1"
//	DYNAMIC: "SKINNING"				"0..1"

#include "common_vs_fxc.h"
#include "common_morphing_vs_fxc.h"

static const bool g_bSkinning = SKINNING ? true : false;

#if ( MORPHING )
// NOTE: cMorphTargetTextureDim.xy = target dimensions,
//		 cMorphTargetTextureDim.z = 4tuples/morph
const float3 cMorphTargetTextureDim			: register( SHADER_SPECIFIC_CONST_6 );
const float4 cMorphSubrect					: register( SHADER_SPECIFIC_CONST_7 );

sampler2D morphSampler : register( D3DVERTEXTEXTURESAMPLER0, s0 );
#endif

#if ( TREESWAY )
	const float4 g_vTreeSwayParams0			: register( SHADER_SPECIFIC_CONST_2 );
	const float4 g_vTreeSwayParams1			: register( SHADER_SPECIFIC_CONST_3 );
	const float4 g_vTreeSwayParams2			: register( SHADER_SPECIFIC_CONST_4 );
	const float4 g_vTreeSwayParams3			: register( SHADER_SPECIFIC_CONST_5 );
	const float4 g_vTreeSwayParams4			: register( SHADER_SPECIFIC_CONST_9 );

	#define g_flTime				g_vTreeSwayParams0.x
	#define g_vWindDir				g_vTreeSwayParams0.yz

	#define g_flScrumbleFalloffCurve g_vTreeSwayParams1.x
	#define g_flSwayFalloffCurve	g_vTreeSwayParams1.y
	#define g_flScrumbleSpeed		g_vTreeSwayParams1.z
	#define g_flFastSwaySpeedScale	g_vTreeSwayParams1.w


	#define g_flHeight				g_vTreeSwayParams2.x
	#define g_flStartHeight			g_vTreeSwayParams2.y
	#define g_flRadius				g_vTreeSwayParams2.z
	#define g_flStartRadius			g_vTreeSwayParams2.w

	#define g_flSwaySpeed			g_vTreeSwayParams3.x
	#define g_flSwayIntensity		g_vTreeSwayParams3.y
	#define g_flScrumbleWaveCount	g_vTreeSwayParams3.z
	#define g_flScrumbleIntensity	g_vTreeSwayParams3.w

	#define g_flWindSpeedLerpStart	g_vTreeSwayParams4.x
	#define g_flWindSpeedLerpEnd	g_vTreeSwayParams4.y
	
	#include "tree_sway.h"
#endif

struct VS_INPUT
{
	float4 vPos					: POSITION;
	float2 vTexCoord			: TEXCOORD0;
	float4 vBoneWeights			: BLENDWEIGHT;
	float4 vBoneIndices			: BLENDINDICES;

	// Position delta stream
	float3 vPosFlex				: POSITION1;

#if ( MORPHING )
	float vVertexID				: POSITION2;
#endif
};

struct VS_OUTPUT
{
    float4 vProjPos				: POSITION;

	float2 texCoord				: TEXCOORD0;

#if COLOR_DEPTH
	float4 vWorldPos_projPosZ	: TEXCOORD1;
#endif

};

VS_OUTPUT main( const VS_INPUT v )
{
	VS_OUTPUT o = ( VS_OUTPUT )0;
	float3 vWorldPos;
	float4 vPosition = v.vPos;

#if ( MORPHING )
	ApplyMorph( morphSampler, cMorphTargetTextureDim, cMorphSubrect, 
		v.vVertexID, float3(0, 0, 0), vPosition.xyz );
#else
	ApplyMorph( v.vPosFlex, vPosition.xyz );
#endif

	#if ( TREESWAY )
	{
		vPosition.xyz = ComputeTreeSway( vPosition.xyz, g_flTime );
	}
	#endif

	SkinPosition( g_bSkinning, vPosition, v.vBoneWeights, v.vBoneIndices, vWorldPos );

	float4 vProjPos = mul( float4( vWorldPos, 1.0f ), cViewProj );

	o.vProjPos    = vProjPos;

	o.texCoord   = v.vTexCoord;

#if ( COLOR_DEPTH )
	o.vWorldPos_projPosZ.z = vProjPos.z;
	o.vWorldPos_projPosZ.w = vProjPos.w;
#endif

	return o;
}


